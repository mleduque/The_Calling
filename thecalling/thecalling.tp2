BACKUP ~weidu_external/thecalling/backup~ // location to store files for
SUPPORT ~http://gibberlings3.net/forums/index.php?showforum=201~ // forum url

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @4 END // Modmerge check

  ACTION_IF NOT VARIABLE_IS_SET tutu_gen THEN BEGIN // just do this part once per install

    INCLUDE ~thecalling/lib/game_checks.tpa~
    
  END  

  ACTION_IF (!tutu_gen AND !game_is_bgt AND !game_is_bgee AND !game_is_eet) THEN BEGIN FAIL @0 END

  ACTION_IF NOT VARIABLE_IS_SET tutu_var THEN BEGIN // just do this part once per install

    // covers STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH
    APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~ UNLESS ~^0x00000FC0[ %TAB]+STATE_REALLY_DEAD[ %TAB%%LNL%%MNL%%WNL%]~
    
    // new state as shortcut for unavailable for dialogue; covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH,
    // STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING
    APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~^0x80101FEF[ %TAB]+CD_STATE_NOTVALID[ %TAB%%LNL%%MNL%%WNL%]~

    INCLUDE ~thecalling/lib/g3_cpmvars_master.tpa~
    APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~ 

    ACTION_IF enhanced_edition THEN BEGIN
      
      OUTER_SPRINT ~tra_location~ ~thecalling/languages~     
      LOAD_TRA ~thecalling/languages/%LANGUAGE%/ee.tra~      
    
   END ELSE BEGIN    
      
      OUTER_SPRINT ~tra_location~ ~weidu_external/thecalling/languages~      
      INCLUDE ~thecalling/lib/tob2soa.tpa~    

      // convert strings from UTF-8 for originals  
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN weidu ee END // strings exclusively for the installer and EE, respectively
      ACTION_DEFINE_ARRAY cdreload BEGIN game END // actual content strings
      LAF HANDLE_CHARSETS INT_VAR from_utf8 = 1 infer_charsets = 1 
                          STR_VAR default_language = ~english~ tra_path = ~thecalling/languages~ out_path = ~weidu_external/thecalling/languages~ noconvert_array = cdnoconvert reload_array = cdreload END

    END

  END

END

VERSION ~v6_preview3~
README ~thecalling/readme-thecalling-%LANGUAGE%.html~ ~thecalling/readme-thecalling.html~
AUTO_TRA ~%tra_location%/%s~

LANGUAGE ~English~ ~english~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
LANGUAGE ~Czech (Translation by Razfallow and Vlasák)~ ~czech~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/czech/game.tra~
  ~thecalling/languages/czech/weidu.tra~
LANGUAGE ~Francais (Traduction : Anomaly, Elgaern the Dragon Slayer, Isaya, elminster and JohnBob)~ ~french~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/french/game.tra~
  ~thecalling/languages/french/weidu.tra~
LANGUAGE ~Deutsch (Uebersetzung von Leonardo Watson, Caswallon und Jester)~ ~german~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/german/game.tra~
  ~thecalling/languages/german/weidu.tra~
LANGUAGE ~Italiano (translation by Andrea Colombo and improb@bile)~ ~italian~
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/italian/game.tra~
  ~thecalling/languages/italian/weidu.tra~
LANGUAGE ~Korean (Translation by web2air)~ ~korean~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/korean/game.tra~
  ~thecalling/languages/korean/weidu.tra~
LANGUAGE ~Polski (Translation by memory, yarpen, Artanis, dragionian, Evendur, Grjgori, and Neminus)~ ~polish~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/polish/game.tra~
  ~thecalling/languages/polish/weidu.tra~
LANGUAGE ~Russian (Translation by Creepin, Domi, Kulyok, Serdrick and aerie.ru; paladin84 and arcanecoast.ru)~ ~russian~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/russian/game.tra~
  ~thecalling/languages/russian/weidu.tra~
LANGUAGE ~Espanol (traducido por Clan DLAN and alaisFcZ)~ ~spanish~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/spanish/game.tra~
  ~thecalling/languages/spanish/weidu.tra~
LANGUAGE ~Simplified Chinese (Translation from bg2_tweaks by Miranda, distance20 and c4_angel，new entry by Lewis Liu)~ ~schinese~ 
  ~thecalling/languages/english/game.tra~
  ~thecalling/languages/english/weidu.tra~
  ~thecalling/languages/schinese/game.tra~
  ~thecalling/languages/schinese/weidu.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Mage - The Cursed Bracers                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @100 DESIGNATED 0
REQUIRE_PREDICATE (("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "french" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "polish" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "russian" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "schinese" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0)) @5 // skip if translation doesn't exist
LABEL ~cd_the_calling_mage~

/////                                                  \\\\\
///// the mage calling                                 \\\\\
/////                                                  \\\\\

/*

*CDBracerQuest

*1 quest accepted, promps meli cutscene
*2 after cutscene
*3 in thalantyr's post-cutscene dlg
*4 one gem returned
*5 two gems in
*6 all three gems, go see taerom
7 taerom met, go see grace at mines
8 grace spoken with
9 meli spawned in nashkel
10 miners fixed, meli returning, go speak to taerom again
11 device in progress
*12 device ready
13 melicamp-nashkel stuff skipped
14 melicamp returns device
15 thalantyr does divination
*16 divination cutscene done
17 go get bracers from halflings
18 halfling cutscene, dialogue; goes to 19 or 80
*19 halflings sent to high hedge
20 incantation complete, go to cutscene
*21 cutscene complete
22 bracers removed badly, quest over
23 bracers removed, take 'em to taerom
24 spoken to taerom, start cutscene
25 cutscene end, last taerom dialogue
26 quest over, completed successfully

98 refused quest
99 melicamp gone on quest instead
*/

// extend scripts for areas
EXTEND_BOTTOM ~%HighHedge_ThalanthyrsAbode_BCS%.bcs~   ~thecalling/baf/_ar3202.baf~ EVALUATE_BUFFER // thalantyr does stuff
EXTEND_BOTTOM ~%Beregost_ThunderhammerSmithy_BCS%.bcs~ ~thecalling/baf/_ar3301.baf~ // taerom cutscene
EXTEND_BOTTOM ~%UlcasterRuins_BCS%.bcs~                ~thecalling/baf/_ar3901.baf~ // add note
EXTEND_BOTTOM ~%Gullykin_House1_L1_BCS%.bcs~           ~thecalling/baf/_ar4005.baf~ // spawn iris & largo
EXTEND_BOTTOM ~%NashkelMines_BCS%.bcs~                 ~thecalling/baf/_ar5400.baf~ // spawn grace
EXTEND_BOTTOM ~%NashkelMines_L1_BCS%.bcs~              ~thecalling/baf/_ar5401.baf~ // spawn melicamp, miners

STRING_SET  7133 @126 // pearl > white pearl
STRING_SET 16380 @111 // add scrying stuff to descript of zircon

// start with dupe dialogues for the halfling couple
COPY_EXISTING ~%tutu_var%halffg.dlg~ ~override/cdiris.dlg~
              ~%tutu_var%halfgu.dlg~ ~override/cdlargo.dlg~

ACTION_IF game_is_bgt BEGIN OUTER_SPRINT thalan_states "1 35" END ELSE BEGIN OUTER_SPRINT thalan_states "1" END
              
ACTION_IF enhanced_edition BEGIN // on EEs, have Thalantyr identify the Black Pearl before giving it to player in case Anthology's 'gems require ID is installed'

  OUTER_SPRINT give_pearl ~CreateItem("cdbpearl",1,0,0) SetItemFlags("cdbpearl",IDENTIFIED,TRUE) GiveItem("cdbpearl",LastTalkedToBy(Myself))~

END ELSE BEGIN

  OUTER_SPRINT give_pearl ~GiveItemCreate("cdbpearl",LastTalkedToBy(Myself),1,0,0)~

END

COMPILE EVALUATE_BUFFER ~thecalling/dlg/mage.d~
                        ~thecalling/baf/cdiris.baf~
                        ~thecalling/baf/cdlargo.baf~
                        ~thecalling/baf/cdminer1.baf~
              
// melicamp needed for quests, so make sure he always survives anti-chickenator
ACTION_IF enhanced_edition BEGIN // ees move the cutscene to the dialogue, so

  OUTER_SET cd_journal1 = 27256
  ACTION_IF game_is_eet BEGIN OUTER_SET cd_journal1 += 200000 END // bg strings in eet are +200000
  OUTER_SET cd_journal2 = cd_journal1 + 1
  OUTER_SET cd_journal3 = cd_journal1 + 2
  
  COMPILE ~thecalling/dlg/melicamp_lives.d~ EVALUATE_BUFFER

END ELSE BEGIN

  COPY_EXISTING ~%tutu_scriptt%halantr.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Kill("melicamp")~ ~~
    END
    BUT_ONLY

END

// add some black pearls to the game
EXTEND_TOP    ~%tutu_scripts%irspell.bcs~             ~thecalling/baf/_irspell.baf~ EVALUATE_BUFFER // give sirines a chance to have black pearls (including sil)
EXTEND_BOTTOM ~%Lighthouse_BlackAlaricsCave_BCS%.bcs~ ~thecalling/baf/pearl.baf~    EVALUATE_BUFFER // guarantee a black pearl in sil's cave
 
ACTION_FOR_EACH bam IN cmisc5c cmisc5k cmisc7p imisc5k imisc5z imisc7p BEGIN

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~%bam%.bam~ THEN BEGIN // missing bams from bgee

    COPY ~thecalling/bam/%bam%.bam~ ~override~
  
  END

END

// scrying device
COPY ~thecalling/itm/cddevice.itm~ ~override~
  SAY 0x08 @106
  SAY 0x0c @106
  SAY 0x50 @118
  SAY 0x54 @118

// taerom's ore
COPY ~thecalling/itm/cdore.itm~ ~override~
  SAY 0x08 @112
  SAY 0x0c @112
  SAY 0x54 @113

// the note about zircons
COPY ~thecalling/itm/cdzrnote.itm~ ~override~
  SAY 0x08 @116
  SAY 0x0c @116
  SAY 0x50 @117
  SAY 0x54 @117

// amulet of acid resistance
COPY ~thecalling/bam/cdamacid.bam~ ~override~
COPY ~thecalling/itm/cdamacid.itm~ ~override~
  SAY 0x0c @119
  SAY 0x54 @120

// amulet of constriction
COPY ~thecalling/itm/cdamcons.itm~ ~override~
  SAY 0x0c @121
  SAY 0x54 @122

// imbecile's amulet
COPY ~thecalling/itm/cdamduh.itm~ ~override~
  SAY 0x0c @123
  SAY 0x54 @124

// black pearls
COPY ~thecalling/bam/cdbpearl.bam~ ~override~
COPY_EXISTING ~misc36.itm~ ~override/cdbpearl.itm~
  SAY 0x0c @125
  SAY 0x54 @102
  WRITE_LONG  0x34 300 // adjust price
  WRITE_ASCII 0x3a ~cdbpearl~ // bam

// magic powder
COPY ~thecalling/bam/cdpowder.bam~ ~override~
COPY ~thecalling/itm/cdpowder.itm~ ~override~
  SAY 0x08 @109
  SAY 0x0c @109
  SAY 0x50 @110
  SAY 0x54 @110

// the bracers
COPY_EXISTING ~brac11.itm~ ~override/cdbracer.itm~
  SAY 0x08 @114
  SAY 0x0c @114
  SAY 0x50 @115
  SAY 0x54 @115
  WRITE_LONG  0x1e 0xffffffff // unusable by all
  WRITE_LONG  0x34  0 // price
  WRITE_SHORT 0x42  0 // lore

// taerom doesn't have a DV (!)
COPY_EXISTING ~%tutu_var%taerom.cre~ ~override~
  WRITE_ASCII 0x2cc ~taerom~ #32
  BUT_ONLY

// nashkel mines melicamp
COPY_EXISTING ~%tutu_var%melica.cre~ ~override/cdmeli.cre~
  WRITE_SHORT  0x28 20992 // human animation, not chicken
  WRITE_ASCII 0x250 ~cdmeli~ #8 // class script
  WRITE_ASCII 0x2cc ~cdmeli~ #8 // dialogue

// miners, halflings
COPY_EXISTING ~%tutu_var%minern.cre~ ~override/cdminer1.cre~
              ~%tutu_var%minern.cre~ ~override/cdminer2.cre~
              ~%tutu_var%halffg.cre~ ~override/cdiris.cre~
              ~%tutu_var%halfgu.cre~ ~override/cdlargo.cre~
  WRITE_ASCIIE 0x248 ~%DEST_RES%~       #8 // override script
  WRITE_ASCIIE 0x250 ~%tutu_var%shout~  #8 // class script
  WRITE_ASCIIE 0x280 ~%DEST_RES%~      #32 // DV
  WRITE_ASCIIE 0x2cc ~%DEST_RES%~       #8 // dialogue

COPY_EXISTING ~cdminer1.cre~ ~override~
  SAY 0x08 @103
  SAY 0x0c @103
  WRITE_ASCII SCRIPT_DEFAULT "wtrunsgt"

COPY_EXISTING ~cdminer2.cre~ ~override~
  SAY 0x08 @104
  SAY 0x0c @104
  WRITE_ASCII SCRIPT_OVERRIDE ~cdminer1~  #8 // override script
  WRITE_ASCII SCRIPT_DEFAULT "wtrunsgt"

COPY_EXISTING ~cdiris.cre~  ~override~
  SAY 0x08 @107
  SAY 0x0c @107

COPY_EXISTING ~cdlargo.cre~ ~override~
  SAY 0x08 @108
  SAY 0x0c @108

// copy laola's soundset for grace
COPY_EXISTING ~%tutu_var%laola.cre~ ~override~
  READ_ASCII 0xa4 "soundset" (396)
  BUT_ONLY

// grace
COPY ~thecalling/cre/cdgrace.cre~ ~override~
  SAY 0x08 @105
  SAY 0x0c @105
  WRITE_ASCIIE 0xa4 ~%soundset%~
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~ #8
  ADD_CRE_ITEM ~%tutu_var%SW1H07~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~%tutu_var%LEAT01~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~%tutu_var%MISC16~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Fighter - Expanded Gnoll Stronghold              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10000 DESIGNATED 10
REQUIRE_PREDICATE ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) @5 // skip if translation doesn't exist
LABEL ~cd_the_calling_fighter~

/* CDGnollQuest

1 - spoken with Captain Stilind, agreed to help
2 - killed the thre gnolls and reported back to Stilind
3 - Stilind turns off the spawn points and moves deeper into the fortress
4 - stilind asks party to get supplies from nashkel
5 - party gives supplies to stilind, cutscene
6 - custscene ends, reinforcements have arrived
7 - post-cutscene dialogue complete
8 - asked to clear out ghost
9 - reported defeat of ghost to stilind, quest complete
*/

/////                                                  \\\\\
///// preamble                                         \\\\\
/////                                                  \\\\\

DEFINE_PATCH_FUNCTION tutuize_items 
  INT_VAR item_number = 0
          item_offset = 0
          item_length = 0x14
  BEGIN
  
  FOR (index = 0 ; index < item_number ; ++index) BEGIN
    READ_ASCII (item_offset + (index * item_length)) item
    PATCH_IF (("%item%" STRING_COMPARE_REGEXP "^[Cc][Dd].+$") AND
              ("%item%" STRING_COMPARE_REGEXP "^[Rr][Nn][Dd][Tt][Rr][Ee].+$") AND
              ("%item%" STRING_COMPARE_CASE "helmnoan") AND
              ("%item%" STRING_COMPARE_CASE "misc07")) BEGIN
      WRITE_ASCIIE (item_offset + (index * item_length)) ~%tutu_var%%item%~ #8
    END
  END
    
END

DEFINE_PATCH_MACRO eetize_soundsets BEGIN
  FOR (snd_off = 0xa4 ; snd_off < 0x231 ; snd_off = snd_off + 0x04) BEGIN
    READ_LONG snd_off strref 
    PATCH_IF strref >=0 BEGIN
      WRITE_LONG snd_off (THIS + 200000)
    END
  END    
END

OUTER_SET opp_palette = 0
ACTION_IF enhanced_edition OR (MOD_IS_INSTALLED ~1pp.tp2~ 102) OR (MOD_IS_INSTALLED ~1pp.tp2~ 102) BEGIN OUTER_SET opp_palette = 1 END
						
ADD_JOURNAL TITLE (@10173) @10174 @10175 @10176 @10177 @10178 @10179 @10180 @10181 @10182 @10183 @10184 @10185 @10186

/////                                                  \\\\\
///// basic copies                                     \\\\\
/////                                                  \\\\\

LAF HANDLE_AUDIO STR_VAR audio_path = ~thecalling/audio/10~ oggdec_path = ~thecalling/audio~ sox_path = ~thecalling/audio~ END // ambients, audio

ACTION_IF !enhanced_edition BEGIN

  APPEND ~tooltip.2da~ ~cd51sta 15529 12033 -1
cd51rew 14034 -1 -1~                           // add tooltip for Staff of Corrosive Thoughts +1, Cloak of the Regent-Warrior
  COPY ~thecalling/spl/cd51rew.spl~ ~override~ // provides fighter-only 19 CHR for Cloak of the Regent-Warrior
  COMPILE ~thecalling/baf/cd5104.baf~          // smoke ambients look terrible in original, disable until fixed
       
END  

// the %enhanced_edition% is used to grab one-cycle item icons (ee) or 2-cycle (non-EE)
COPY ~thecalling/bam/cd51amu%enhanced_edition%.bam~ ~override/cd51amu.bam~ // icon, Amulet of Dark Misfortune
     ~thecalling/bam/cd51arc%enhanced_edition%.bam~ ~override/cd51arc.bam~ // description image, Broken Spirit Arrow +1
     ~thecalling/bam/cd51ari%enhanced_edition%.bam~ ~override/cd51ari.bam~ // icon, Broken Spirit Arrow +1
     ~thecalling/bam/cd51bel%enhanced_edition%.bam~ ~override/cd51bel.bam~ // icon, Belt of Omens
     ~thecalling/bam/cd51cha%enhanced_edition%.bam~ ~override/cd51cha.bam~ // icon, Chain of Primitive Respect +1
     ~thecalling/bam/cd51clo%enhanced_edition%.bam~ ~override/cd51clo.bam~ // icon, Cloak of Shape Stability
     ~thecalling/bam/cd51key%enhanced_edition%.bam~ ~override/cd51key.bam~ // icon, Primitive Key
     ~thecalling/bam/cd51rew%enhanced_edition%.bam~ ~override/cd51rew.bam~ // icon, Cloak of the Regent-Warrior
     ~thecalling/bam/cd51sec%enhanced_edition%.bam~ ~override/cd51sec.bam~ // icon, Yeenoghu's Burning Flesh
     ~thecalling/bam/cd51smk.bam~                   ~override~             // ambient animation, smoke
     ~thecalling/bam/cd51sta%enhanced_edition%.bam~ ~override/cd51sta.bam~ // icon, Staff of Corrosive Thoughts +1
     ~thecalling/bam/cd51sup%enhanced_edition%.bam~ ~override/cd51sup.bam~ // icon, Medical Supplies
     ~thecalling/eff/cd51rew%enhanced_edition%.eff~ ~override/cd51rew.eff~ // provides fighter-only 19 CHR for Cloak of the Regent-Warrior

COPY ~thecalling/sto/cd51shop.sto~ ~override~
  SAY 0x1c @10172
  PATCH_IF tutu_gen BEGIN // adjust tutu resource names
    LPF tutuize_items INT_VAR item_number = (SHORT_AT 0x76) item_offset = (LONG_AT 0x78) item_length = 0x1c END
  END  

/////                                                  \\\\\
///// area work                                        \\\\\
/////                                                  \\\\\

COPY ~thecalling/are/art~ ~override~ // bmp, mos, wed

// new areas that connect to the gnoll stronghold need to use the platform-specific resref
COPY ~thecalling/are/cd5104.are~ ~override~
     ~thecalling/are/cd5105.are~ ~override~
     ~thecalling/are/cd5106.are~ ~override~
     ~thecalling/are/cd5107.are~ ~override~
     ~thecalling/are/cd5108.are~ ~override~
     ~thecalling/are/cd5109.are~ ~override~
  LPF ALTER_AREA_REGION STR_VAR region_name = to_gnoll_exterior destination_area = EVAL ~%GnollStronghold%~ END 
  PATCH_IF tutu_gen BEGIN // adjust tutu resource names
    LPF tutuize_items INT_VAR item_number = (LONG_AT 0x38) item_offset = (LONG_AT 0x34) END
  END  

COPY_EXISTING ~cd5104.are~ ~override~
  PATCH_IF tutu_gen BEGIN // adjust tutu resource names
    LPF ALTER_AREA_REGION STR_VAR region_name = Trap1 region_script = EVAL ~%tutu_var%gtar~ END
    LPF ALTER_AREA_REGION STR_VAR region_name = Trap2 region_script = EVAL ~%tutu_var%gtar~ END
    LPF ALTER_AREA_REGION STR_VAR region_name = Trap3 region_script = EVAL ~%tutu_var%gtfb~ END
    LPF ALTER_AREA_REGION STR_VAR region_name = Trap4 region_script = EVAL ~%tutu_var%gtar~ END
    LPF ALTER_AREA_CONTAINER STR_VAR container_name = Container002 container_script  = EVAL ~%tutu_var%ctar~ END
    LPF ALTER_AREA_CONTAINER STR_VAR container_name = Container004 container_script  = EVAL ~%tutu_var%ctlb~ END
    LPF ALTER_AREA_CONTAINER STR_VAR container_name = Container039 container_script  = EVAL ~%tutu_var%ctar~ END
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = dog cre_file = EVAL ~%tutu_var%dogwi~ END
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = spider cre_file = EVAL ~%tutu_var%spidgi~ END  
  END  
  
COPY_EXISTING ~cd5105.are~ ~override~
  LPF ALTER_AREA_REGION STR_VAR region_name = to_gnoll_exterior2 destination_area = EVAL ~%GnollStronghold%~ END 
  PATCH_IF tutu_gen BEGIN // adjust tutu resource names
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Actor 1" cre_file = EVAL ~%tutu_var%gnoll~ END
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Actor 26" cre_file = EVAL ~%tutu_var%flind~ END
    READ_LONG 0xc0 rest_off
    FOR (index = 0 ; index < 10 ; ++index) BEGIN 
      READ_ASCII   (rest_off + 0x48 + (index * 0x04)) mon (7)
      WRITE_ASCIIE (rest_off + 0x48 + (index * 0x04)) ~_%mon%~ #8 // can do this lazily since the fields are known
    END
  END ELSE
  PATCH_IF game_is_eet BEGIN
    READ_LONG 0xc0 rest_off
    FOR (index = 0 ; index < 10 ; ++index) BEGIN 
      WRITE_LONG (rest_off + 0x20 + (index * 0x04)) THIS + 200000 // update rest spawn strings 
    END
  END
  
COPY_EXISTING ~cd5106.are~ ~override~
  PATCH_IF tutu_gen BEGIN // adjust tutu resource names
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Actor 1" cre_file = EVAL ~%tutu_var%flind~ END
    LPF ALTER_AREA_REGION STR_VAR region_name = Trap1 region_script = EVAL ~%tutu_var%gtfb~ END
  END  

COPY ~thecalling/tilesets/ar5100sr.bmp~  ~override/%GnollStronghold%sr.bmp~ // stronghold searchmap; give new entrances a little more walking room

ACTION_IF enhanced_edition BEGIN

  // add new areas to the console menu listing
  COPY_EXISTING ~bgee.lua~ ~override~
    SPRINT cd5104_descript @10023 // do it this way so that the entries can also be translated
    SPRINT cd5105_descript @10026
    SPRINT cd5106_descript @10162
    SPRINT cd5107_descript @10163
    SPRINT cd5108_descript @10164 
    SPRINT cd5109_descript @10165 
    REPLACE_TEXTUALLY ~\({"%GnollStronghold_CrawlerCave%",.+},\)~ ~\1
	{"cd5104", "%cd5104_descript%"},
	{"cd5105", "%cd5105_descript%"},
	{"cd5106", "%cd5106_descript%"},
	{"cd5107", "%cd5107_descript%"},
	{"cd5108", "%cd5108_descript%"},
	{"cd5109", "%cd5109_descript%"},~
  
  COPY ~thecalling/tilesets/new~            ~override~                       // new area tilesets/pvrz
       ~thecalling/tilesets/old/ar5100.tis~ ~override/%GnollStronghold%.tis~ // updated gnoll stronghold
       
  ACTION_IF game_is_bgee BEGIN OUTER_SPRINT prefix ~a~ END ELSE BEGIN OUTER_SPRINT prefix ~b~ END
  OUTER_FOR (index = 510000; index < 510022 ; ++index) BEGIN
    COPY ~thecalling/tilesets/old/a%index%.pvrz~ ~override/%prefix%%index%.pvrz~
  END  
  
END ELSE BEGIN

  INCLUDE ~thecalling/lib/a7_tools.tpa~ // tile2ee
  LAF HANDLE_TILE2EE INT_VAR quality_v1 = 9 STR_VAR input_path = ~thecalling/tilesets/new~ END // new area tilesets/pvrz
  
  COPY ~thecalling/tilesets/ar5100.mos~     ~override/%GnollStronghold%.mos~ // minimap 
  
  LAF HANDLE_TILESETS STR_VAR input_path = EVAL ~thecalling/tiz/%tutorbgt%~ END

END

COPY_EXISTING ~%GnollStronghold%.are~ ~override~ // adding entrances to main gnoll stronghold map
  //DUNGEON
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 2977
    fj_box_top = 2261
    fj_box_right = 3059
    fj_box_bottom = 2378
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 3053
    fj_alt_y = 2390
    fj_loc_x = 3053
    fj_loc_y = 2390
    fj_vertex_0 = 2977 + (2378 << 16)
    fj_vertex_1 = 2977 + (2321 << 16)
    fj_vertex_2 = 2997 + (2301 << 16)
    fj_vertex_3 = 3006 + (2291 << 16)
    fj_vertex_4 = 3010 + (2266 << 16)
    fj_vertex_5 = 3015 + (2261 << 16)
    fj_vertex_6 = 3037 + (2313 << 16)
    fj_vertex_7 = 3052 + (2333 << 16)
    fj_vertex_8 = 3059 + (2342 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_GnollUnd
    fj_destination_area = CD5104
    fj_destination_name = En
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 3053
    fj_loc_y = 2390
    fj_orientation = 14 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5104E
  END
  //MAIN - archway entrance
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 2211
    fj_box_top = 1652
    fj_box_right = 2315
    fj_box_bottom = 1794
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 2235
    fj_alt_y = 1799
    fj_loc_x = 2235
    fj_loc_y = 1799
    fj_vertex_0 = 2211 + (1745 << 16)
    fj_vertex_1 = 2211 + (1669 << 16)
    fj_vertex_2 = 2230 + (1652 << 16)
    fj_vertex_3 = 2250 + (1652 << 16)
    fj_vertex_4 = 2281 + (1667 << 16)
    fj_vertex_5 = 2301 + (1688 << 16)
    fj_vertex_6 = 2315 + (1716 << 16)
    fj_vertex_7 = 2314 + (1776 << 16)
    fj_vertex_8 = 2309 + (1794 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_GnollMain
    fj_destination_area = CD5105
    fj_destination_name = en_arch
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 2235
    fj_loc_y = 1799
    fj_orientation = 2 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5105E_arch
  END
  //MAIN - back entrance
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 2103
    fj_box_top = 1352
    fj_box_right = 2253
    fj_box_bottom = 1459
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 2213
    fj_alt_y = 1362
    fj_loc_x = 2213
    fj_loc_y = 1362
    fj_vertex_0 = 2143 + (1352 << 16)
    fj_vertex_1 = 2103 + (1413 << 16)
    fj_vertex_2 = 2197 + (1459 << 16)
    fj_vertex_3 = 2253 + (1403 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_GnollMain2
    fj_destination_area = CD5105
    fj_destination_name = En1
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 2258
    fj_loc_y = 1312
    fj_orientation = 10 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5105E
  END
  //MINI
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 1870
    fj_box_top = 660
    fj_box_right = 1920
    fj_box_bottom = 768
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 1858
    fj_alt_y = 802
    fj_loc_x = 1858
    fj_loc_y = 802
    fj_vertex_0 = 1870 + (745 << 16)
    fj_vertex_1 = 1883 + (732 << 16)
    fj_vertex_2 = 1910 + (681 << 16)
    fj_vertex_3 = 1914 + (660 << 16)
    fj_vertex_4 = 1917 + (679 << 16)
    fj_vertex_5 = 1920 + (768 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_GnollMini
    fj_destination_area = CD5107
    fj_destination_name = En
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 1858
    fj_loc_y = 802
    fj_orientation = 2 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5107E
  END
  // upper area, left to 5108
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 1655
    fj_box_top = 1119
    fj_box_right = 1767
    fj_box_bottom = 1283
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 1718
    fj_alt_y = 1141
    fj_loc_x = 1718
    fj_loc_y = 1141
    fj_vertex_0 = 1655 + (1119 << 16)
    fj_vertex_1 = 1655 + (1224 << 16)
    fj_vertex_2 = 1767 + (1283 << 16)
    fj_vertex_3 = 1767 + (1129 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_5108
    fj_destination_area = CD5108
    fj_destination_name = En
    fj_reg_script = cdgnsen1
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 1733
    fj_loc_y = 1092
    fj_orientation = 8 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5108E
  END
  // upper area, right to 5109
  LPF fj_are_structure INT_VAR
    fj_type = 2 // travel
    fj_box_left = 2696
    fj_box_top = 1470
    fj_box_right = 2877
    fj_box_bottom = 1579
    fj_cursor_idx = 42
    fj_flags = 516 // no_NPCs = 512, party_required = save = 4
    fj_alt_x = 2800
    fj_alt_y = 1504
    fj_loc_x = 2800
    fj_loc_y = 1504
    fj_vertex_0 = 2758 + (1470 << 16)
    fj_vertex_1 = 2696 + (1579 << 16)
    fj_vertex_2 = 2815 + (1566 << 16)
    fj_vertex_3 = 2877 + (1527 << 16)
  STR_VAR
    fj_structure_type = region
    fj_name = CD_5108
    fj_destination_area = CD5109
    fj_destination_name = En
    fj_reg_script = cdgnsen2
  END
  LPF fj_are_structure INT_VAR
    fj_loc_x = 2837
    fj_loc_y = 1470
    fj_orientation = 9 // facing direction (0-15 where 0=south, 4=west, 8=north, 12=east)*;
  STR_VAR
    fj_structure_type = entrance
    fj_name = CD5109E
  END
  PATCH_IF game_is_bgt OR MOD_IS_INSTALLED ~cdtweaks.tp2~ ~1110~ BEGIN // BGT has map notes, Tutu gets them via Tweaks
    ADD_MAP_NOTE #3053  #2390 ~gray~   #193 // Cave
    ADD_MAP_NOTE #1858   #802 ~gray~ @10017 // Nook
    ADD_MAP_NOTE #2235  #1799 ~red~  @10020 // Archway
    ADD_MAP_NOTE #2258  #1312 ~red~  #61443 // stairs down
    ADD_MAP_NOTE #1733  #1092 ~red~  #61443 // stairs down
    ADD_MAP_NOTE #2837  #1470 ~red~  #61443 // stairs down
  END  

/////                                                  \\\\\
///// creatures                                        \\\\\
/////                                                  \\\\\

COPY ~thecalling/cre/cd51ch.cre~ ~override~
  SAY NAME1 @10005
  SAY NAME2 @10005
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  
  PATCH_IF !opp_palette BEGIN // if using color only available with 1pp/EE
    WRITE_BYTE 0x2e 46
  END  

COPY ~thecalling/cre/cd51gho.cre~ ~override~
  SAY NAME1 @10010
  SAY NAME2 @10010
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  

COPY ~thecalling/cre/cd51go.cre~ ~override~
  SAY NAME1 @10006
  SAY NAME2 @10006
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  

COPY ~thecalling/cre/cd51gs.cre~ ~override~
  SAY NAME1 @10003
  SAY NAME2 @10003
  PATCH_IF !enhanced_edition BEGIN // no shaman, so delete shaman-only spells and give generic druid AI script
    WRITE_BYTE 0x273 11 // class: druid
    LPF DELETE_EFFECT INT_VAR match_opcode = 251 END
    REMOVE_MEMORIZED_SPELL sppr150 sppr250 sppr350
    REMOVE_KNOWN_SPELL     sppr150 sppr250 sppr350  
    WRITE_ASCII 0x268 ~druid8b~   
    PATCH_IF !opp_palette BEGIN // if using color only available with 1pp/EE
      WRITE_BYTE 0x31 58
    END                   	   
  END  

COPY ~thecalling/cre/cd51gw.cre~ ~override~
  SAY NAME1 @10002
  SAY NAME2 @10002
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  

COPY ~thecalling/cre/cd51gz.cre~ ~override~
  SAY NAME1 @10004
  SAY NAME2 @10004
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  

COPY ~thecalling/cre/cd51qm.cre~ ~override~
  SAY NAME1 @10007
  SAY NAME2 @10007
  WRITE_ASCIIE 0x258 ~%tutu_var%shout~  
  WRITE_ASCIIE 0x268 ~%tutu_scriptw%tasight~  

COPY ~thecalling/cre/cd51sol1.cre~ ~override~
     ~thecalling/cre/cd51sol2.cre~ ~override~
  SAY NAME1 @10008
  SAY NAME2 @10008
  WRITE_ASCIIE 0x258 ~%tutu_var%shout~  
  WRITE_ASCIIE 0x268 ~%tutu_scripts%eeenemy~  

COPY ~thecalling/cre/cd51sol3.cre~ ~override~
  SAY NAME1 @10009
  SAY NAME2 @10009
  WRITE_ASCIIE 0x258 ~%tutu_var%shout~  
  WRITE_ASCIIE 0x268 ~%tutu_scripts%eeenemy~   

COPY_EXISTING ~cd51sol1.cre~ ~override/cd51shop.cre~
  SAY NAME1 @10172
  SAY NAME2 @10172
  WRITE_ASCII 0x280 cd51shop // dv
  WRITE_ASCII 0x2cc cd51shop // dlg
  
ACTION_IF tutu_gen BEGIN // adjust tutu item names

  COPY_EXISTING ~cd51ch.cre~   ~override~
                ~cd51gho.cre~  ~override~
                ~cd51go.cre~   ~override~
                ~cd51gs.cre~   ~override~
                ~cd51gw.cre~   ~override~
                ~cd51gz.cre~   ~override~
                ~cd51qm.cre~   ~override~  
                ~cd51sol1.cre~ ~override~
                ~cd51sol2.cre~ ~override~
                ~cd51sol3.cre~ ~override~   
    LPF tutuize_items INT_VAR item_number = (LONG_AT 0x2c0) item_offset = (LONG_AT 0x2bc) END
    BUT_ONLY
    
  ACTION_IF !FILE_EXISTS_IN_GAME ~_halb07.itm~ BEGIN  
    COPY_EXISTING ~halb07.itm~ ~override/_halb07.itm~    
  END

END
  
ACTION_IF game_is_eet BEGIN // adjust strings for EET soundsets

  COPY_EXISTING ~cd51ch.cre~   ~override~
                ~cd51gho.cre~  ~override~
                ~cd51go.cre~   ~override~
                ~cd51gs.cre~   ~override~
                ~cd51gw.cre~   ~override~
                ~cd51gz.cre~   ~override~
                ~cd51qm.cre~   ~override~  
                ~cd51sol1.cre~ ~override~
                ~cd51sol2.cre~ ~override~
                ~cd51sol3.cre~ ~override~   
    LPM eetize_soundsets
    BUT_ONLY

END

/////                                                  \\\\\
///// scripts & dialogue                               \\\\\
/////                                                  \\\\\

COMPILE ~thecalling/baf/cd51qm.baf~
        ~thecalling/baf/cd51ch.baf~
        ~thecalling/baf/cd51go.baf~
        ~thecalling/baf/cd51gs.baf~
        ~thecalling/baf/cd5107.baf~
COMPILE ~thecalling/baf/cdgnsen1.baf~ 
        ~thecalling/baf/cdgnsen2.baf~ 
        ~thecalling/baf/cd51alt.baf~ EVALUATE_BUFFER

EXTEND_BOTTOM ~%GnollStronghold_BCS%.bcs~ ~thecalling/baf/ar5100.baf~         EVALUATE_BUFFER
EXTEND_TOP    ~%tutu_var%xvart.bcs~       ~thecalling/baf/gnolldestroyer.baf~ EVALUATE_BUFFER // destroys leftover gnolls/xvarts at end of quest
EXTEND_TOP    ~%tutu_var%gnoll.bcs~       ~thecalling/baf/gnolldestroyer.baf~ EVALUATE_BUFFER
EXTEND_TOP    ~%tutu_var%gnollf.bcs~      ~thecalling/baf/gnolldestroyer.baf~ EVALUATE_BUFFER
EXTEND_TOP    ~%tutu_var%gnollfb.bcs~     ~thecalling/baf/gnolldestroyer.baf~ EVALUATE_BUFFER

COMPILE ~thecalling/dlg/cd51gs.d~
COMPILE ~thecalling/dlg/cd51qm.d~ EVALUATE_BUFFER
COMPILE ~thecalling/dlg/cd51ch.d~
COMPILE ~thecalling/dlg/cd51go.d~
COMPILE ~thecalling/dlg/cd51sol.d~
COMPILE ~thecalling/dlg/cd51sol3.d~
COMPILE ~thecalling/dlg/cd51shop.d~
COMPILE ~thecalling/dlg/nalin.d~ EVALUATE_BUFFER

/////                                                  \\\\\
///// items                                            \\\\\
/////                                                  \\\\\

COPY ~thecalling/itm/cd51amu.itm~ ~override~
  SAY NAME1 #6913
  SAY NAME2 @10021
  SAY DESC @10022

COPY ~thecalling/itm/cd51ari.itm~ ~override~
  SAY NAME1 #6328
  SAY NAME2 @10024
  SAY DESC @10025
  PATCH_IF !enhanced_edition BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 319 END
  END  

COPY ~thecalling/itm/cd51bel.itm~ ~override~
  SAY NAME1 #12704
  SAY NAME2 @10012
  SAY DESC @10013

COPY ~thecalling/itm/cd51boo.itm~ ~override~
  SAY NAME1 @10031
  SAY NAME2 @10031
  SAY DESC @10032

COPY ~thecalling/itm/cd51cha.itm~ ~override~
  SAY NAME1 #214
  SAY NAME2 @10015
  SAY DESC @10016
  PATCH_IF !opp_palette BEGIN // if using color only available with 1pp/EE
    LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 4 parameter1 = 52 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 7 match_parameter2 = 5 parameter1 = 53 END
  END                   	   

COPY ~thecalling/itm/cd51clo.itm~ ~override~
  SAY NAME1 #12004
  SAY NAME2 @10018
  SAY DESC @10019

COPY ~thecalling/itm/cd51key.itm~ ~override~
  SAY NAME1 @10038
  SAY NAME2 @10038
  SAY DESC @10039

COPY ~thecalling/itm/cd51rew%enhanced_edition%.itm~ ~override/cd51rew.itm~
  SAY NAME1 #12004
  SAY NAME2 @10029
  PATCH_IF enhanced_edition BEGIN
    SAY DESC @10030
  END ELSE BEGIN  
    SAY DESC @10031
  END  

COPY ~thecalling/itm/cd51sec.itm~ ~override~
  SAY NAME1 @10035
  SAY NAME2 @10036
  SAY DESC @10037

// staff adds +10% dmaage to acid on EE; casts melf's acid arrow 1/day on non-EE
COPY ~thecalling/itm/cd51sta%enhanced_edition%.itm~ ~override/cd51sta.itm~
  SAY NAME1 #6351
  SAY NAME2 @10011
  PATCH_IF enhanced_edition BEGIN
    SAY DESC @10028
  END ELSE BEGIN 
    SAY DESC @10027
    PATCH_IF MOD_IS_INSTALLED ~1pp.tp2~ 207 BEGIN // wizard quarterstaves
      WRITE_ASCII 0x22 ~q3~ #2
    END  
  END  

COPY ~thecalling/itm/cd51sup.itm~ ~override~
  SAY NAME1 @10033
  SAY NAME2 @10033
  SAY DESC @10034

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// unlimited callings                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @50000 DESIGNATED 50
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~thecalling.tp2~ 10) OR
                   (MOD_IS_INSTALLED ~thecalling.tp2~ 20)) @6 // skip if no callings installed
LABEL ~cd_the_calling_cheats~

EXTEND_BOTTOM ~baldur.bcs~ ~thecalling/baf/cheat2.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Peaceful Kaishas resolution                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @20000 DESIGNATED 20
REQUIRE_PREDICATE (("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "french" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "german" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "polish" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "russian" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "schinese" = 0) OR 
                   ("%LANGUAGE%" STRING_COMPARE_CASE "spanish" = 0)) @5 // skip if translation doesn't exist
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ ~7060~ @1 // skip if already installed by scs
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @2 // totsc required
LABEL ~cd_the_calling_peaceful_kaishas_resolution~

EXTEND_BOTTOM ~%tutu_var%kaisha.bcs~      ~thecalling/baf/kaishas.baf~ EVALUATE_BUFFER USING ~%tra_location%/%LANGUAGE%/werewolf.tra~
EXTEND_TOP    ~%IsleofBalduranN_BCS%.bcs~ ~thecalling/baf/werewolfadd.baf~ EVALUATE_BUFFER

COPY_EXISTING ~%tutu_var%werewo.cre~ ~override/dw#wwolf.cre~
  WRITE_ASCII 0x248 ~dw#hlpka~ #8
  
COPY_EXISTING ~%tutu_var%ship.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Dead("kaishwlf")~ ~OR(2)Dead("kaishwlf")Global("DMWWNoKillKaishas","GLOBAL",1)~
  END
  BUT_ONLY  

COMPILE EVALUATE_BUFFER ~thecalling/dlg/werewolf.d~
                        ~thecalling/baf/dw#hlpka.baf~
                        ~thecalling/baf/dw#kcut1.baf~
                        ~thecalling/baf/dw#kcut2.baf~
                        ~thecalling/baf/dw#kcut3.baf~
                        
ACTION_IF game_is_eet BEGIN
						
  ADD_JOURNAL EXISTING TITLE (#231414) @21614 @21648 @21637 @21638 

END ELSE BEGIN
						
  ADD_JOURNAL EXISTING TITLE (#31414) @21614 @21648 @21637 @21638 

END

ACTION_IF game_is_bgt BEGIN // bgt doesn't assign kaishas to kaish2

  COPY_EXISTING ~kaish2.cre~ ~override~
    WRITE_ASCII 0x250 ~kaisha~ #8
    BUT_ONLY
  
END  
					

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// The Movanic Cult                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

//BEGIN @30000 DESIGNATED 30 DEPRECATED @3
//LABEL ~cd_the_calling_movanic_cult~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Exotic Items Pack                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @40000 DESIGNATED 40
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~CDTWEAKS/SETUP-CDTWEAKS.TP2~ ~1090~ @1 // skip if already installed by tweaks anthology
LABEL ~cd_the_calling_exotic_items~

ACTION_IF enhanced_edition THEN BEGIN // BGEE, eet
  
  // since adding tsu-o-shi to krumm overwrites his club +1, find new home for it
  COPY_EXISTING ~caldo.cre~ ~override~ // move club +1 to krumm's companion
    REPLACE_CRE_ITEM ~blun36~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
    // match proficiency for new weapon
    READ_BYTE 0x72 prof_blunt
    READ_BYTE 0x73 prof_spike
    PATCH_IF prof_spike > prof_blunt BEGIN
      WRITE_BYTE 0x72 prof_spike
    END

END

COMPILE ~thecalling/dlg/exotic.d~ EVALUATE_BUFFER // fixes Gullykin cleric's dlg file to open new store

ACTION_IF NOT FILE_EXISTS_IN_GAME ~%tutu_var%tem4003.sto~ THEN BEGIN // if Gullykin does not have a store already

  COPY_EXISTING ~%tutu_var%tem4802.sto~ ~override/%tutu_var%tem4003.sto~ // clones and renames Temple of Helm
    SAY 0x0c @40022

END

// artwork
COPY ~thecalling/bam/ccdioun1.bam~ ~override~
     ~thecalling/bam/cdiammo.bam~  ~override~
     ~thecalling/bam/cdipotbg.bam~ ~override~
     ~thecalling/bam/cdpammo.bam~  ~override~
     ~thecalling/bam/cdplbag1.bam~ ~override~
     ~thecalling/bam/cdplbag2.bam~ ~override~
     ~thecalling/bam/cdplqiv1.bam~ ~override~
     ~thecalling/bam/cdplqiv2.bam~ ~override~
     ~thecalling/bam/cdppotbg.bam~ ~override~
     ~thecalling/bam/icdioun1.bam~ ~override~
     ~thecalling/bam/icdioun2.bam~ ~override~
     ~thecalling/bam/icdkat2.bam~  ~override~

// add and name items
INCLUDE ~thecalling/lib/cd_functions.tpa~
LAF cd_make_bag_from_ammo INT_VAR name = RESOLVE_STR_REF(@40001) desc = RESOLVE_STR_REF(@40002)                                  // create quiver of plenty +0 
                          STR_VAR source = EVAL ~%tutu_var%arow01~ output = cdplquiv icon_i = ~cdplqiv1~ icon_c = ~cdplqiv2~ END // from basic arrows
LAF cd_make_bag_from_ammo INT_VAR name = RESOLVE_STR_REF(@40003) desc = RESOLVE_STR_REF(@40004)                                  // create case of plenty +0
                          STR_VAR source = EVAL ~%tutu_var%bolt01~ output = cdplcase icon_i = ~cdplqiv1~ icon_c = ~cdplqiv2~ END // from basic bolts
LAF cd_make_bag_from_ammo INT_VAR name = RESOLVE_STR_REF(@40005) desc = RESOLVE_STR_REF(@40006)                                  // create bag of plenty +0
                          STR_VAR source = EVAL ~%tutu_var%bull01~ output = cdplbag  icon_i = ~cdplbag2~ icon_c = ~cdplbag1~ END // from basic bullets
 
COPY_EXISTING ~helm18.itm~ ~override/cdioun1.itm~ // create ioun stones from existing stone
              ~helm18.itm~ ~override/cdioun2.itm~
  WRITE_ASCIIE 0x3a ~i%DEST_RES%~ #8 // icon, inventory
  WRITE_ASCII  0x58 ~ccdioun1~    #8 // icon, description
  WRITE_LONG   0x4c 2                // weight  
  LPF DELETE_EFFECT END // delete all effects
  
COPY_EXISTING ~cdioun1.itm~ ~override~  // Deep Purple Ioun Stone
  SAY 0x0c @40007
  SAY 0x54 @40008
  WRITE_LONG 0x34 600 // price
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 63 target = 1 timing = 2 END // add infravision
  
COPY_EXISTING ~cdioun2.itm~ ~override~  // Flickering White Ioun Stone
  SAY 0x0c @40009
  SAY 0x54 @40010
  WRITE_LONG 0x34 1200 // price
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode =  28 target = 1 parameter1 = 10 timing = 2 END // add cold resistance
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = 25 timing = 2 END // add cold resistance portrait icon

COPY_EXISTING ~sw1h55.itm~ ~override/cdkat2.itm~ // create tsu-o-shi from generic katana +2
  SAY 0x0c @40013
  SAY 0x54 @40014
  WRITE_LONG  0x18 (THIS BOR BIT4) // adds cursed flag
  WRITE_LONG  0x34 1               // price
  WRITE_SHORT 0x42 100             // lore
  WRITE_ASCII 0x3a ~icdkat2~ #8    // icon, inventory
  LPF ALTER_HEADER STR_VAR icon = icdkat2 END // icon, ability headers
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 16 parameter1 = 45 END // color: head/blade/staff major
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 20 parameter1 = 44 END // color: grip/staff minor
  LPF ALTER_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 21 parameter1 = 64 END // color: head/blade minor
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = 35 timing = 2 insert_point = 999 END // display cursed portrait icon
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40018) timing = 1 probability1 =  4 probability2 =  0 END // charisma modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =   6 target = 1 parameter1 = "-1"                    timing = 1 probability1 =  4 probability2 =  0 END // charisma modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40016) timing = 1 probability1 =  9 probability2 =  5 END // constitution modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  10 target = 1 parameter1 = "-1"                    timing = 1 probability1 =  9 probability2 =  5 END // constitution modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40017) timing = 1 probability1 = 14 probability2 = 10 END // dexterity modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  15 target = 1 parameter1 = "-1"                    timing = 1 probability1 = 14 probability2 = 10 END // dexterity modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40020) timing = 1 probability1 = 19 probability2 = 15 END // intelligence modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  19 target = 1 parameter1 = "-1"                    timing = 1 probability1 = 19 probability2 = 15 END // intelligence modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40021) timing = 1 probability1 = 24 probability2 = 20 END // strength modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  44 target = 1 parameter1 = "-1"                    timing = 1 probability1 = 24 probability2 = 20 END // strength modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40015) timing = 1 probability1 = 29 probability2 = 25 END // wisdom modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  49 target = 1 parameter1 = "-1"                    timing = 1 probability1 = 29 probability2 = 25 END // wisdom modification
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 1 parameter1 = RESOLVE_STR_REF(@40019) timing = 1 probability1 = 34 probability2 = 30 END // hit point modification string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode =  18 target = 1 parameter1 = "-5"                    timing = 1 probability1 = 34 probability2 = 30 END // hit point modification

EXTEND_BOTTOM ~%CloakwoodMines_L4_BCS%.bcs~           ~thecalling/baf/_ar1803.baf~ // adds Contagion scroll to Davaeorn's loot
EXTEND_BOTTOM ~%Lighthouse_BlackAlaricsCave_BCS%.bcs~ ~thecalling/baf/_ar3601.baf~ // adds Ninja-To +0 to Sirine cave loot

COPY_EXISTING ~%tutu_var%meilum.cre~ ~override~ // adds katana +0 to Meilum by replacing primary weapon
  REPLACE_CRE_ITEM ~sw1h43~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP

COPY_EXISTING ~%tutu_var%krumm.cre~  ~override~ // adds katana +2 to Krumm by replacing primary weapon
  REPLACE_CRE_ITEM ~cdkat2~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x72 prof_blunt
  PATCH_IF prof_blunt > prof_lsword BEGIN
    WRITE_BYTE 0x6e prof_blunt
  END

COPY_EXISTING ~%tutu_var%davaeo.cre~ ~override~ // adds Flickering White Ioun stone to helmet slot
  ADD_CRE_ITEM ~cdioun2~ #0 #0 #0 ~NONE~ ~HELMET~

COPY_EXISTING ~%tutu_var%haseo.cre~  ~override~ // adds katana +1 to Haseo by moving primary weapon to offhand
  REPLACE_CRE_ITEM ~sw1h44~           #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM     ~%tutu_var%sw1h05~ #0 #0 #0 ~NONE~ ~SHIELD~

COPY_EXISTING ~%tutu_var%desret.cre~ ~override~ // adds wakizashi +1 to Desreta by moving primary weapon to offhand
  REPLACE_CRE_ITEM ~sw1h47~           #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM     ~%tutu_var%sw1h05~ #0 #0 #0 ~NONE~ ~SHIELD~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_lsword > prof_ssword BEGIN
    WRITE_BYTE 0x6f prof_lsword
  END

COPY_EXISTING ~%tutu_var%hakt.cre~   ~override~ // adds wakizashi +0 to Hakt by replacing primary melee weapon
  REPLACE_CRE_ITEM ~sw1h46~ #0 #0 #0 ~NONE~ ~WEAPON2~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_lsword > prof_ssword BEGIN
    WRITE_BYTE 0x6f prof_lsword
  END

COPY_EXISTING ~%tutu_var%maneir.cre~ ~override~ // adds scimitar +1 to Maneira by replacing primary melee weapon
  REPLACE_CRE_ITEM ~%tutu_var%sw1h22~ #0 #0 #0 ~NONE~ ~WEAPON2~
  READ_BYTE 0x6e prof_lsword // proficiency update since weapon type changes
  READ_BYTE 0x6f prof_ssword
  PATCH_IF prof_ssword > prof_lsword BEGIN
    WRITE_BYTE 0x6e prof_ssword
  END

COPY_EXISTING ~%tutu_scriptbg%sendai%eet_var%.cre~ ~override~ // adds scimitar +1 to Sendai Argrim by replacing offhand weapon
  REPLACE_CRE_ITEM ~%tutu_var%sw1h22~ #0 #0 #0 ~NONE~ ~SHIELD~

COPY_EXISTING ~%tutu_scripts%toblack.sto~ ~override~  // adds Ninja-To +1 to Black Lily's store
  ADD_STORE_ITEM ~sw1h49~ AFTER  ~sw1h07 _sw1h07~ #0 #0 #0 ~IDENTIFIED~ #1

COPY_EXISTING ~%tutu_scripth%ighhedg.sto~ ~override~  // adds lev 1, 2 scrolls to High Hedge
              ~%tutu_var%sto0703.sto~     ~override~  // adds lev 1, 2 scrolls to Sorcerous Sundries
  ADD_STORE_ITEM ~scrl6d~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // find familiar
  ADD_STORE_ITEM ~scrl5u~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // reflected image
  ADD_STORE_ITEM ~scrla6~ AFTER ~scrl70 _scrl70~ #0 #0 #0 ~IDENTIFIED~ #2 // spook
  ADD_STORE_ITEM ~scrl6e~ AFTER ~scrl89 _scrl89~ #0 #0 #0 ~IDENTIFIED~ #2 // pw: sleep

COPY_EXISTING ~%tutu_var%sto0703.sto~ ~override~  // adds lev 3, 4 scrolls to Sorcerous Sundries
  ADD_STORE_ITEM ~scrl6l~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // hold undead
  ADD_STORE_ITEM ~scrl6i~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // protection from cold
  ADD_STORE_ITEM ~scrla5~ AFTER ~scrl1k _scrl1k~ #0 #0 #0 ~IDENTIFIED~ #2 // MMM
  ADD_STORE_ITEM ~scrl6r~ AFTER ~scrl5i _scrl5i~ #0 #0 #0 ~IDENTIFIED~ #2 // spider spawn

COPY_EXISTING ~%tutu_scripth%ighhedg.sto~ ~override~ // High Hedge
  ADD_STORE_ITEM ~cdioun1~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // purple ioun stone

COPY_EXISTING ~%tutu_var%taerum.sto~    ~override~ // Thunderhammer Smithy
  ADD_STORE_ITEM ~cdplcase~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // case of plenty

COPY_EXISTING ~%tutu_var%sto4803.sto~ ~override~ // Nashkel General Store
  ADD_STORE_ITEM ~cdplbag~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // bag of plenty

COPY_EXISTING ~%tutu_var%sto4909.sto~   ~override~ // Carnival
  ADD_STORE_ITEM ~cdplquiv~ LAST #0 #0 #0 ~IDENTIFIED~ #1 // quiver of plenty

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~  // adds generic scimitars to stores that sell short swords if not present already
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  READ_LONG 0x2c itm_off   ELSE 0
  READ_LONG 0x4c drink_off ELSE 0
  READ_LONG 0x70 cure_off  ELSE 0
  SET scimitar_insert = 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?[Ss][Ww]1[Hh]07$" = 0) BEGIN // short sword
      READ_ASCII (sale_off +        (index * 0x1c)) clone (28) // read short sword entry
      SET scimitar_insert = (index + 1) // sets right after as insert point
    END ELSE
    PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?[Ss][Ww]1[Hh]20$" = 0) BEGIN // scimitar
      SET scimitar_insert = 0 // scimitars are already present
      SET index = sale_num    // kills loop
    END
  END
  PATCH_IF (scimitar_insert > 0) BEGIN // short swords present but not scimitars
    INSERT_BYTES   (sale_off + (scimitar_insert * 0x1c)) 28
      WRITE_ASCIIE (sale_off + (scimitar_insert * 0x1c)) ~%clone%~              // clones short sword entry
      WRITE_ASCIIE (sale_off + (scimitar_insert * 0x1c)) ~%tutu_var%sw1h20~ #8  // scimitar
    // correcting offsets and such
    WRITE_LONG 0x38 (sale_num + 1)
    PATCH_FOR_EACH off IN 0x2c 0x4c 0x70 BEGIN // item, drink, cure offsets
      READ_LONG off val                        // read offset
      PATCH_IF val > sale_off BEGIN            // if after for-sale list...
        WRITE_LONG off (THIS + 0x1c)           // push back for new scimitar
      END
    END
  END
  BUT_ONLY

// if Ashes of Embers is installed, the katana and quivers should be usable by all
ACTION_IF ((FILE_EXISTS_IN_GAME ~j#swsoa.txt~) OR                        // SoA version of AoE (for BGT)
           (FILE_EXISTS_IN_GAME ~J#ToBAoE.txt~) OR                       // Tutu version of AoE
           (FILE_EXISTS_IN_GAME ~j#sensibleweaponsmods.txt~)) THEN BEGIN // mod-only version of AoE

  COPY_EXISTING ~cdplquiv.itm~ ~override~ // quiver of plenty +0
                ~cdplcase.itm~ ~override~ // case of plenty +0
                ~cdplbag.itm~  ~override~ // bag of plenty +0
                ~cdkat2.itm~   ~override~ // katana +2
    WRITE_LONG 0x1e 0 // remove all class/race/align unusabilities
    WRITE_BYTE 0x29 0 // remove all kit unusabilities
    WRITE_BYTE 0x2b 0 // remove all kit unusabilities
    WRITE_BYTE 0x2d 0 // remove all kit unusabilities
    WRITE_BYTE 0x2f 0 // remove all kit unusabilities

  COPY_EXISTING ~cdkat2.itm~ ~override~   // katana +2
    WRITE_BYTE 0x2b BIT1 // remove all kit unusabilities except for Beastmaster

END
